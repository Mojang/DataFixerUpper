apply plugin: 'java-library'
apply plugin: 'maven-publish'

group = 'com.mojang'
version = project.hasProperty('buildNumber') ? "${project.majorMinor}.${project.buildNumber}" : "${project.majorMinor}.0-SNAPSHOT"
def javaMajorVersion = project.hasProperty('javaMajorVersion') ? "${project.javaMajorVersion}" : "17"

sourceCompatibility = "${javaMajorVersion}"
targetCompatibility = "${javaMajorVersion}"

buildscript {
    repositories {
        mavenCentral()
        maven {
            url "https://libraries.minecraft.net"
        }
    }
}

repositories {
    maven {
        url "https://libraries.minecraft.net"
    }
    mavenCentral()
}

dependencies {
    api 'com.google.code.gson:gson:2.8.9' // JsonElement and the rest used and exposed in JsonOps
    api 'com.google.guava:guava:31.0.1-jre' // TypeToken primarily exposed
    api 'it.unimi.dsi:fastutil:8.5.6' // various collection types are used in public API

    compileOnlyApi 'com.google.code.findbugs:jsr305:3.0.2'
    implementation 'org.slf4j:slf4j-api:1.8.0-beta4'

    testImplementation 'junit:junit-dep:4.10'
    testImplementation "org.slf4j:slf4j-simple:1.8.0-beta4"
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

sourceSets {
    main {
        java {
            srcDirs = ['src/main/java']
        }
        resources {
            srcDirs = ['src/main/resources']
        }
    }
    test {
        java {
            srcDirs = ['src/test/java']
        }
        resources {
            srcDirs = ['src/test/resources']
        }
    }
}

artifacts {
    archives jar
    archives sourcesJar
}

test {
    testLogging {
        events "failed", "skipped"
        showStandardStreams = true
        showExceptions true
    }
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
            artifact sourcesJar
        }
    }
}

task report {
    doLast {
        println "##vso[build.updatebuildnumber]${project.version}"
    }
}

def publishDir = file("$buildDir/repo")


def uploadFile(s3, bucket, path, filename) {
    println "Uploading $filename to $bucket as $path"
    s3.putObject(bucket, path, filename)
}

clean.doLast {
    delete publishDir
}

if (version.endsWith("SNAPSHOT")) {
    publishing.repositories {
        mavenLocal()
    }
} else {
    publishing.repositories {
        maven {
            url "$buildDir/repo"
        }
    }

    publish.doLast {
        publishDir.eachFileRecurse {
            if (!it.isFile()) {
                return
            }
            // Remove junk files
            if (it.name.contains(".xml") || it.name.contains(".md5")) {
                it.delete()
            }
        }
    }
}
